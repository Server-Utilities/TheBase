plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6' // for shadowing
    id 'io.freefair.lombok' version '8.10.2'
}

group = 'com.github.server-utilities'
version = '1.0.0'

java.sourceCompatibility = JavaVersion.VERSION_11
java.targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

configurations {
    implementation {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.apache.logging.log4j'
    }
    shadow {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.apache.logging.log4j'
    }
}

dependencies {
//    implementation 'me.nobeld:SimplixStorage:3.3.0-beta.3'
    implementation 'com.github.DragonFixes:SimplixStorage:master-SNAPSHOT'
    shadow 'com.github.DragonFixes:SimplixStorage:master-SNAPSHOT'
    implementation 'org.jetbrains:annotations:24.0.1'
    shadow 'org.jetbrains:annotations:24.0.1'
    implementation 'mysql:mysql-connector-java:8.0.33'
    shadow 'mysql:mysql-connector-java:8.0.33'
    implementation 'org.xerial:sqlite-jdbc:3.46.1.0'
    shadow 'org.xerial:sqlite-jdbc:3.46.1.0'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    shadow 'com.zaxxer:HikariCP:5.1.0'
    implementation 'net.objecthunter:exp4j:0.4.8'
    shadow 'net.objecthunter:exp4j:0.4.8'
    implementation 'com.google.re2j:re2j:1.7'
    shadow 'com.google.re2j:re2j:1.7'
}

shadowJar {
    Map<String, String> relocations = [
            'de.leonhard': 'gg.drak.thebase.lib.leonhard',
            'com.mysql': 'gg.drak.thebase.lib.mysql',
            'com.zaxxer.hikari': 'gg.drak.thebase.lib.hikari',
            'net.objecthunter.exp4j': 'gg.drak.thebase.lib.exp4j',
            'com.google.re2j': 'gg.drak.thebase.lib.re2j',
            'com.google.gson': 'gg.drak.thebase.lib.google.gson',
            'com.google.guava': 'gg.drak.thebase.lib.google.guava',
            'org.sqlite': 'gg.drak.thebase.lib.sqlite',
            'org.xerial': 'gg.drak.thebase.lib.xerial',
            'org.apache.commons': 'gg.drak.thebase.lib.apache.commons',
            'org.yaml': 'gg.drak.thebase.lib.yaml',
            'org.bstats': 'gg.drak.thebase.lib.bstats'
    ]
    relocations.each { src, dest -> relocate src, dest }

    exclude 'com/google/common/**'
    exclude 'org/apache/logging/log4j/**'
    exclude 'org/slf4j/**'
    exclude 'META-INF/**'
    exclude '**/LICENSE'
    exclude '**/README.md'
    exclude '**/docs/**'

    archiveFileName = "${project.name}-${project.version}.jar"
}

artifacts {
    archives shadowJar
}

java {
    withSourcesJar()
    withJavadocJar()
}

wrapper {
    gradleVersion = '8.9'
    distributionType = Wrapper.DistributionType.BIN
}