plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6'
    id 'io.freefair.lombok' version '8.10.2'
    id 'maven-publish'
}

group = 'com.github.server-utilities'
version = '1.0.0'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

configurations {
    implementation {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.apache.logging.log4j'
    }
    shadow {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.apache.logging.log4j'
    }
}

dependencies {
    implementation "me.nobeld:SimplixStorage:3.3.0-beta.3"
    implementation 'mysql:mysql-connector-java:8.0.33'
    implementation 'org.xerial:sqlite-jdbc:3.46.1.0'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'net.objecthunter:exp4j:0.4.8'
    implementation 'com.google.re2j:re2j:1.7'
}

shadowJar {
    configurations = [project.configurations.implementation, project.configurations.shadow]
    Map<String, String> relocations = [
            'de.leonhard': 'gg.drak.thebase.lib.leonhard',
            'com.mysql': 'gg.drak.thebase.lib.mysql',
            'com.zaxxer.hikari': 'gg.drak.thebase.lib.hikari',
            'net.objecthunter.exp4j': 'gg.drak.thebase.lib.exp4j',
            'com.google.re2j': 'gg.drak.thebase.lib.re2j',
            'com.google.gson': 'gg.drak.thebase.lib.google.gson',
            'com.google.guava': 'gg.drak.thebase.lib.google.guava',
            'org.sqlite': 'gg.drak.thebase.lib.sqlite',
            'org.xerial': 'gg.drak.thebase.lib.xerial',
            'org.apache.commons': 'gg.drak.thebase.lib.apache.commons',
            'org.yaml': 'gg.drak.thebase.lib.yaml',
            'org.bstats': 'gg.drak.thebase.lib.bstats'
    ]
    relocations.each { src, dest -> relocate src, dest }

    exclude 'com/google/common/**'
    exclude 'org/apache/logging/log4j/**'
    exclude 'org/slf4j/**'
    exclude 'META-INF/**'
    exclude '**/LICENSE'
    exclude '**/README.md'
    exclude '**/docs/**'

    archiveFileName = "${project.name}-${project.version}.jar"
}

tasks.register('deploy', Copy) {
    def deployDir = file(System.getenv('DEPLOY_DIR') ?: "$rootDir/deploy")
    from shadowJar.archiveFile
    into deployDir

    doFirst {
        println "Deploying to: $deployDir"
        deployDir.mkdirs()
        println "Generated JAR file: ${shadowJar.archiveFile.get()}"
    }
}

tasks.named('deploy').configure {
    dependsOn 'shadowJar', 'sourcesJar', 'javadocJar', 'jar'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

wrapper {
    gradleVersion = '8.9'
    distributionType = Wrapper.DistributionType.BIN
}